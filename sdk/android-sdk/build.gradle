apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.noveogroup.android.check'
apply plugin: 'idea'
apply plugin: 'kotlin-android'

ext {
    groupId = "eu.intent"
    artifactId = "android-sdk"

    libraryName = "Intent Android SDK"
    libraryDescription = "An SDK to use the Intent API in your Android apps."
    libraryVersion = "2.0"

    developerId = "Intent Technologies"
    developerName = "Intent Technologies"
    developerEmail = "support@intent-technologies.eu"
}

group = groupId
version = libraryVersion

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError true
        // We try to use the latest API and dependencies, but allow to build with older versions
        disable 'GradleDependency', 'OldTargetApi'
        // Retrofit needs packages that are not declared
        disable 'InvalidPackage'
        // The Activities in this library are not declared in AndroidManifest.xml, the client applications will declare them in their own AndroidManifest.xml
        disable 'Registered'
    }
}

check {
    abortOnError true
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.3.2"
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
    if (JavaVersion.current().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                groupId groupId
                artifactId artifactId

                name libraryName
                description libraryDescription

                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
            }
        }
    }
}

// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        userOrg = "intent-technologies"
        repo = "maven"
        name = "intent-sdk-android"
        desc = libraryDescription
        publish = true
    }
}
buildscript {
}
repositories {
    mavenCentral()
}
